plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'kotlin-android'
}

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'origin/main'
            standardOutput = code
        }
        return Integer.parseInt(code.toString().trim())
    }
    catch (ignored) {
        return 1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim().replaceFirst("^v", "")
    }
    catch (ignored) {
        return '0.0.0'
    }
}

android {
    compileSdk = 36
    defaultConfig {
        applicationId "top.rootu.lampa"
        minSdk = 28
        versionCode getVersionCode()
        versionName getVersionName()
        // Use SupportLibrary for vectors
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropsFile = file("keystore/keystore_config")

            if (keystorePropsFile.exists()) {
                file("keystore/keystore_config").withInputStream { keystoreProperties.load(it) }
                storeFile file("$keystoreProperties.storeFile")
                storePassword "$keystoreProperties.storePassword"
                keyAlias "$keystoreProperties.keyAlias"
                keyPassword "$keystoreProperties.keyPassword"
            } else {
                def keystoreFile = System.getenv('KEYSTORE_FILE')
                if (keystoreFile != null) {
                    storeFile file(keystoreFile)
                    storePassword System.getenv('KEYSTORE_PASSWORD')
                    keyAlias System.getenv('RELEASE_SIGN_KEY_ALIAS')
                    keyPassword System.getenv('RELEASE_SIGN_KEY_PASSWORD')
                }
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packagingOptions {
        resources.excludes += "DebugProbesKt.bin"
    }

    flavorDimensions = ["default"]

    productFlavors {
        lite {
            dimension "default"
            buildConfigField "boolean", "enableUpdate", "true"
            buildConfigField "String", "defaultAppUrl", "\"\""
        }
        full {
            dimension "default"
            buildConfigField "boolean", "enableUpdate", "false"
            buildConfigField "String", "defaultAppUrl", "\"\""
        }
        ruStore {
            dimension "default"
            versionNameSuffix "-RuStore"
            buildConfigField "boolean", "enableUpdate", "false"
            buildConfigField "String", "defaultAppUrl", "\"http://lampa.mx\""
        }
    }

    buildFeatures {
        buildConfig = true
        viewBinding true
    }
    namespace 'top.rootu.lampa'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def actKtxVer = '1.9.0'
    implementation "androidx.activity:activity-ktx:$actKtxVer"
    def appCompatVer = '1.7.0'
    implementation "androidx.appcompat:appcompat:$appCompatVer"
    def constraintVersion = '2.2.0'
    implementation "androidx.constraintlayout:constraintlayout:${constraintVersion}"
    def coreKtxVer = '1.15.0'
    implementation "androidx.core:core-ktx:$coreKtxVer"
    def lifecycleVersion = '2.8.7'
    implementation "androidx.lifecycle:lifecycle-process:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recommendation:recommendation:1.0.0'
    implementation 'androidx.tvprovider:tvprovider:1.0.0'
    def vectordrawableVersion = '1.2.0'
    implementation "androidx.vectordrawable:vectordrawable:${vectordrawableVersion}"
    def webkitVersion = '1.12.1'
    implementation "androidx.webkit:webkit:$webkitVersion"
    def glideVersion = '5.0.5'
    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    implementation("com.github.bumptech.glide:okhttp3-integration:${glideVersion}") {
        exclude group: 'glide-parent'
        transitive = false
    }
    ksp "com.github.bumptech.glide:ksp:${glideVersion}"
    implementation "com.github.bumptech.glide:annotations:${glideVersion}"
    def muiVersion = '1.12.0'
    implementation "com.google.android.material:material:${muiVersion}"
    def okhttpVersion = '4.12.0'
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-dnsoverhttps:$okhttpVersion"
    implementation 'org.brotli:dec:0.1.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kp_version"
    def coroutinesVersion = '1.9.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation 'com.google.code.gson:gson:2.11.0'
    def lottieVersion = '6.6.2'
    implementation "com.airbnb.android:lottie:$lottieVersion"
    implementation 'info.guardianproject.netcipher:netcipher:2.1.0'
    implementation 'junit:junit:4.13.2'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'
    implementation 'org.mozilla:rhino:1.8.0'
}
